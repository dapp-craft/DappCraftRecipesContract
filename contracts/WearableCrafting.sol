pragma solidity ^0.5.12;

import "openzeppelin-solidity/contracts/ownership/Ownable.sol";
import 'multi-token-standard/contracts/interfaces/IERC1155TokenReceiver.sol';
import "./Strings.sol";
import "multi-token-standard/contracts/interfaces/IERC165.sol";

interface ERC721Collection {
    function issueToken(address _beneficiary, string calldata _wearableId) external;
    function getWearableKey(string calldata _wearableId) external view returns (bytes32);
    function issued(bytes32 _wearableKey) external view returns (uint256);
    function maxIssuance(bytes32 _wearableKey) external view returns (uint256);
    function issueTokens(address[] calldata _beneficiaries, bytes32[] calldata _wearableIds) external;
    function balanceOf(address _owner) external view returns (uint256);
}

/**
 * @title WearableCrafting
 * WearableCrafting - Receive ERC1155 voucher and mint ERC721 wearable
 */
contract WearableCrafting is IERC165, IERC1155TokenReceiver, Ownable {
    using Strings for string;

    ERC721Collection erc721WearableCollection;
    address erc1155VouchersCollection;
    mapping (uint256 => string) public wearableIds;

    // onReceive function signatures
    bytes4 constant internal ERC1155_RECEIVED_VALUE = 0xf23a6e61;
    bytes4 constant internal ERC1155_BATCH_RECEIVED_VALUE = 0xbc197c81;

    constructor(
        address _vouchersCollection,
        ERC721Collection _wearableCollection
    ) public {
        erc721WearableCollection = _wearableCollection;
        erc1155VouchersCollection = _vouchersCollection;

        wearableIds[64] = "moonminer_helmet";
        wearableIds[65] = "moonminer_glasses_eyewear";
        wearableIds[66] = "moonminer_jacket_upper_body";
        wearableIds[67] = "moonminer_pants_lower_body";
        wearableIds[68] = "moonminer_boots_feet";
        wearableIds[69] = "moonminer_hairs";
    }

    function mintWearables(address _beneficiary, uint256 _id, uint256 _amount) internal {
        string memory wearableId = wearableIds[_id];
        require(bytes(wearableId).length != 0, "wearableId is not correct");

        for (uint256 i = 0; i < _amount; i++) {
            erc721WearableCollection.issueToken(_beneficiary, wearableId);
        }
    }
    /**
     * @notice Handle the receipt of a single ERC1155 token type
     * @dev An ERC1155-compliant smart contract MUST call this function on the token recipient contract, at the end of a `safeTransferFrom` after the balance has been updated
     * This function MAY throw to revert and reject the transfer
     * Return of other amount than the magic value MUST result in the transaction being reverted
     * Note: The token contract address is always the message sender
     * param _operator  The address which called the `safeTransferFrom` function
     * @param _from      The address which previously owned the token
     * @param _id        The id of the token being transferred
     * @param _amount    The amount of tokens being transferred
     * param _data      Additional data with no specified format
     * @return           `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
     */
    function onERC1155Received(address /*_operator*/, address _from, uint256 _id, uint256 _amount, bytes calldata /*_data*/) external returns(bytes4){
        require(msg.sender == erc1155VouchersCollection, "Only token contract allowed");
        mintWearables(_from, _id, _amount);
        return ERC1155_RECEIVED_VALUE;
    }

    /**
     * @notice Handle the receipt of multiple ERC1155 token types
     * @dev An ERC1155-compliant smart contract MUST call this function on the token recipient contract, at the end of a `safeBatchTransferFrom` after the balances have been updated
     * This function MAY throw to revert and reject the transfer
     * Return of other amount than the magic value WILL result in the transaction being reverted
     * Note: The token contract address is always the message sender
     * param _operator  The address which called the `safeBatchTransferFrom` function
     * @param _from      The address which previously owned the token
     * @param _ids       An array containing ids of each token being transferred
     * @param _amounts   An array containing amounts of each token being transferred
     * param _data      Additional data with no specified format
     * @return           `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
     */
    function onERC1155BatchReceived(address /*_operator*/, address _from, uint256[] calldata _ids, uint256[] calldata _amounts, bytes calldata /*_data*/) external returns(bytes4){
        require(msg.sender == erc1155VouchersCollection, "Only token contract allowed");
        require(_ids.length == _amounts.length, "Arrays must be same size");

        for (uint256 k = 0; k < _ids.length; k++) {
            uint256 id = _ids[k];
            uint256 amount = _amounts[k];
            mintWearables(_from, id, amount);
        }
        return ERC1155_BATCH_RECEIVED_VALUE;
    }

    bytes4 constant private INTERFACE_SIGNATURE_ERC165 = 0x01ffc9a7;
    bytes4 constant private INTERFACE_SIGNATURE_ERC1155TokenReceiver = 0x4fdcdb47;
    /**
     * @notice Indicates whether a contract implements the `ERC1155TokenReceiver` functions and so can accept ERC1155 token types.
     * @param  _interfaceID The ERC-165 interface ID that is queried for support.s
     * @dev This function MUST return true if it implements the ERC1155TokenReceiver interface and ERC-165 interface.
     *      This function MUST NOT consume more than 5,000 gas.
     * @return Wheter ERC-165 or ERC1155TokenReceiver interfaces are supported.
     */
    function supportsInterface(bytes4 _interfaceID) external view returns (bool) {
        if (_interfaceID == INTERFACE_SIGNATURE_ERC165 ||
        _interfaceID == INTERFACE_SIGNATURE_ERC1155TokenReceiver) {
            return true;
        }
        return false;
    }

    function setWearableCollection(
        ERC721Collection _wearableCollection
    ) public onlyOwner {
        erc721WearableCollection = _wearableCollection;
    }

    function setWearableIds(
        uint256 _voucherId,
        string memory _wearableId
    ) public onlyOwner {
        wearableIds[_voucherId] = _wearableId;
    }
}
